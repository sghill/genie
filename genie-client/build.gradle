dependencies {
    /*******************************
     * API Dependencies
     *******************************/

    api project(":genie-common")
    api("com.github.fge:json-patch")
    api("com.squareup.okhttp3:okhttp")
    api("com.squareup.retrofit2:retrofit")

    /*******************************
     * Implementation Dependencies
     *******************************/

    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jdk8")
    implementation("com.squareup.retrofit2:converter-jackson")
    implementation("commons-beanutils:commons-beanutils")
    implementation("org.apache.commons:commons-configuration2")
    implementation("org.slf4j:slf4j-api")

    /*******************************
     * Test Dependencies
     *******************************/

    testCompile project(":genie-test")
    testCompile project(":genie-web")
    testCompile("commons-io:commons-io")
}

clean {
    delete "src/main/python/nflx_genie_client.egg-info"
    delete "src/main/python/dist"
}

task installPythonDependencies(type: Exec) {
    dependsOn clean
    workingDir "src/main/python"
    commandLine "sudo", "pip", "install", "-U", "pip", "setuptools", "twine"
}

task installPythonPackage(type: Exec) {
    dependsOn installPythonDependencies
    workingDir "src/main/python"
    commandLine "sudo", "pip", "install", "-U", "-e", "."
}

task testPythonPackage(type: Exec) {
    dependsOn installPythonPackage
    workingDir "src/main/python"
    commandLine "sh", "-c", "nosetests"
}

task buildPythonPackage(type: Exec) {
    dependsOn clean
    workingDir "src/main/python"
    commandLine "python", "setup.py", "sdist"
}

task uploadToPypi(type: Exec) {
    dependsOn buildPythonPackage
    workingDir "src/main/python"
    commandLine "bash", "-c", "twine upload dist/*"
}
